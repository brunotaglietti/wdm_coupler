## SWG-Based WDM Coupler/Splitter

# Resets the lumerical layout before running
newproject; redrawoff;
filename = "wdm";
save(filename);

wl0 = 1550e-9;

# Sets up the analysis scripts for figure of merit
setnamed("::model", "analysis script",'
tx1 = getresult("P1", "T");
T1 = spline(tx1.T, tx1.lambda, lambda_0);
tx2 = getresult("P2", "T");
T2 = spline(tx2.T, tx2.lambda, lambda_0);
');
setnamed("::model::");
addanalysisprop("lambda_0", 0, wl0);
addanalysisresult("T1");
addanalysisresult("T2");

# Sweeping parameters
wl_sweep = [1550e-9, 1310e-9];
Lc_sweep = linspace(10e-6, 50e-6, 41);
t_sweep = [60e-9, 120e-9, 180e-9];

# Checks if the matrices already exist, in which case leaves them untouch so that the sweep can just skip the iterations already done.
if(!exist('T1') & !exist('T2')){
    T1 = matrix(length(wl_sweep), length(Lc_sweep), length(t_sweep));
    T2 = matrix(length(wl_sweep), length(Lc_sweep), length(t_sweep));
}

# Sets the fundamental parameters as model variables.
Clad_Material = 'SiO2 (Glass) - Palik';
Box_Material = 'SiO2 (Glass) - Palik';
Core_Material = 'Si (Silicon) - Palik';
Lc = max(Lc_sweep);
t = min(t_sweep);
wi = 500e-9;
Lt = 6e-6;
Lb = 10e-6;
ws = 1.0e-6;
Lambda = 200e-9;
a = 100e-9;
thickness = 220e-9;

substrate_length = 100e-6;
substrate_width = 30e-6;

## Running the layout scripts
wdm_coupler_layout;

for(i = 1:length(wl_sweep)){
    for(j = 1:length(Lc_sweep)){
        for(k = 1:length(t_sweep)){
            if(T1(i,j,k) == 0){ # checks if the iteration was already done
                # Sets the parameters to the current iteration
                switchtolayout;
                setnamed("::model::source", "center wavelength", wl_sweep(i));
                setnamed("::model::source", "wavelength span", 1e-9);
                setnamed("::model::", "lambda_0", wl_sweep(i));
                setnamed("::model::WDM Coupler", "Lc", Lc_sweep(j));
                setnamed("::model::WDM Coupler", "t", t_sweep(k));

                # Runs the simulation and grabs results
                run;
                runanalysis;
                T1(i,j,k) = getresult("::model::", "T1");
                T2(i,j,k) = getresult("::model::", "T2");

                # Prints progress
                ?num2str((k + (j-1)*length(t_sweep) + (i-1)*length(Lc_sweep)*length(t_sweep))/(length(wl_sweep)*length(Lc_sweep)*length(t_sweep))*100) + '%';
            } else {
                ?num2str((k + (j-1)*length(t_sweep) + (i-1)*length(Lc_sweep)*length(t_sweep))/(length(wl_sweep)*length(Lc_sweep)*length(t_sweep))*100) + '% skipped!';
            }
        }
    }
}

# Saves the results
matlabsave("./Results/tsweep_3", Lc_sweep, wl_sweep, t_sweep, T1, T2);
