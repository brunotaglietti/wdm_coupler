## SWG-Based WDM Coupler/Splitter

# Resets the lumerical layout before running
newproject; redrawoff; time_str;
filename = "wdm";
save(filename);

wl0 = 1550e-9;

# Sets up the analysis scripts for figure of merit
setnamed("::model", "analysis script",'
tx1a = getresult("P1", "T");
T1a = spline(tx1a.T, tx1a.lambda, 1315e-9);
tx2a = getresult("P2", "T");
T2a = spline(tx2a.T, tx2a.lambda, 1315e-9);

tx1b = getresult("P1", "T");
T1b = spline(tx1b.T, tx1b.lambda, 1555e-9);
tx2b = getresult("P2", "T");
T2b = spline(tx2b.T, tx2b.lambda, 1555e-9);

er_1 = 10*log10(T1b/T1a);
er_2 = 10*log10(T2a/T2b);

ER = er_1*er_2;
');
setnamed("::model::");
addanalysisresult("T1a");
addanalysisresult("T2a");
addanalysisresult("T1b");
addanalysisresult("T2b");
addanalysisresult("ER");
addanalysisresult("er_1");
addanalysisresult("er_2");

# Sweeping parameters
# wl_sweep = [1310e-9, 1550e-9];
# Lc_sweep = linspace(5e-6, 34.4e-6, 161);
# g_sweep = [95e-9, 98e-9];

# Sets the fundamental parameters as model variables.
Clad_Material = 'SiO2 (Glass) - Palik';
Box_Material = 'SiO2 (Glass) - Palik';
Core_Material = 'Si (Silicon) - Palik';
# Lc = max(Lc_sweep);
# g = max(g_sweep);
Lc = 34.4e-6;
g = 98e-9;
wi = 500e-9;
Lt = 6e-6;
Lb = 10e-6;
ws = 1000e-9;
ws2 = 1000e-9;
Lambda = 200e-9;
a = 85e-9;
thickness = 220e-9;

substrate_length = Lc + 2*Lt + Lb + 30e-6;
substrate_width = 30e-6;

## Running the layout scripts
wdm_coupler_layout;

## Resetting for optimized parameters
# setnamed("::model::WDM Coupler", "Lc", 41.1921e-6);
# setnamed("::model::WDM Coupler", "g", 92.1282e-9);
# setnamed("::model::WDM Coupler", "a", 105.665e-9);

## Setting optimization/sweep Parameters and FOM
p1 = struct;
p1.Parameter = "::model::WDM Coupler::Lc";
p1.Type = "Length";
p1.Min = 35e-6;
p1.Max = 50e-6;
p1.Start = p1.Min;
p1.Stop = p1.Max;
p2 = struct;
p2.Parameter = "::model::WDM Coupler::g";
p2.Type = "Length";
p2.Min = 80e-9;
p2.Max = 120e-9;
p3 = struct;
p3.Parameter = "::model::WDM Coupler::a";
p3.Type = "Length";
p3.Min = 70e-9;
p3.Max = 130e-9;
r1 = struct;
r1.Name = "er";
r1.Result = "::model::ER";
r1.Optimize = true;
r2 = struct;
r2.Name = "er1";
r2.Result = "::model::er_1";
r3 = struct;
r3.Name = "er2";
r3.Result = "::model::er_2";

## Adding Sweep
addsweep;
setsweep("sweep", "Type", "Ranges");
setsweep("sweep", "Number of points", 61);
addsweepparameter("sweep", p1);
addsweepresult("sweep", r1);
addsweepresult("sweep", r2);
addsweepresult("sweep", r3);

## Adding Optimization
addsweep(1);
setsweep("optimization", "name", "PSO");
setsweep("PSO", "Type", "Maximize");
setsweep("PSO", "algorithm", "Particle Swarm");
setsweep("PSO", "maximum generations", 1000);
setsweep("PSO", "generation size", 6);
setsweep("PSO", "tolerance", 0);
addsweepparameter("PSO", p1);
addsweepparameter("PSO", p2);
addsweepparameter("PSO", p3);
addsweepresult("PSO", r1);

# wdmc_hard_sweep;
