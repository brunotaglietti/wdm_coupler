deleteall;
##############################################
# Isosceles trapezoid
# An extruded isosceles trapezoid that is symmetric
# about its vertical center line.
#
# Input properties
# index: index of refraction for dielectric
# material: material of object
# z span: thickness of extrusion
# y span: vertical length of trapezoid
# lx top: top length of trapezoid
# lx base: base length of the trapezoid
#
# Tags: polygon extrusion isosceles trapezoid
#
# Copyright 2010 Lumerical Solutions Inc
##############################################
# simplify variable names by removing spaces
z_span = %z span%;
y_span = %y span%;
lx_top = %lx top%;
lx_base = %lx base%;

V=matrix(4,2);
#[x,y] points
V(1,1:2)=[-lx_base/2,-y_span/2];
V(2,1:2)=[-lx_top/2,y_span/2];
V(3,1:2)=[lx_top/2,y_span/2];
V(4,1:2)=[lx_base/2,-y_span/2];

addpoly;
  set("x",0);
  set("y",0);
  set("z",0);
  set("z span",z_span);
  set("vertices",V);
  set("material",material);
  if(get("material")=="<Object defined dielectric>") 
    { set("index",index); } 
     set("override mesh order from material database",1);
  set("mesh order",1);