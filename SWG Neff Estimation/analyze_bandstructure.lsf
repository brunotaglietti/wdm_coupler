# Script to create the band structure diagram, and extract neff, ng.

#substrate material
substrate_material = "SiO2 (Glass) - Palik Dispersive";

# get the spectrum
spectrum = getsweepresult("sweep","spectrum");

#image on a log scale, the portion of the bandstructure we've extracted
image(spectrum.kx,spectrum.f*1e-12,transpose(log10(abs(spectrum.fs))),"k (2pi/a)","f (THz)");


# extract curve of fband vs beta
a = getnamed("::model","pitch");
beta = spectrum.kx*2*pi/a;
fband = matrix(length(spectrum.kx));
for(i=1:length(beta)) {
  peak_pos = findpeaks(abs(spectrum.fs(1:length(spectrum.f),i)));
  fband(i) = spectrum.f(peak_pos);
}

# plot the wavelength versus the k vector value.
plotxy(spectrum.kx, c/fband*1e9,"k (2pi/a)","Wavelength (nm)");


# fit to a (N-1)th order polynomial
N = 3;
w = 2*pi*fband;
p = polyfit(beta,w,N);

# sample the data on a high resolution
beta_high_res = linspace(min(beta),max(beta),1000);
w_high_res = p(1);
for(i=2:length(p)) {
    w_high_res = w_high_res + p(i)*beta_high_res^(i-1);
}

# plot the original data and the fitted result and lightline
lightline_w = linspace(0.95*min(w_high_res),1.05*max(w_high_res),1000);
n_substrate = getfdtdindex(substrate_material,lightline_w/(2*pi),min(spectrum.f),max(spectrum.f));
lightline_beta = lightline_w*real(n_substrate)/c;
plotxy(beta,w,
       beta_high_res,w_high_res,
       lightline_beta,lightline_w,"beta (1/m)","w (rad/s)");
legend("bandstructure data","polynomial fit to data","lightline");

# calculate the phase velocity
v_phase = w_high_res/beta_high_res;

# sample dw/dbeta to get the group velocity
dw_dbeta = p(2);
for(i=3:length(p)) {
    dw_dbeta = dw_dbeta + p(i)*(i-1)*beta_high_res^(i-2);
}
v_group = dw_dbeta;

# calculate neff and ng from the phase and group velocities
neff = c/v_phase;
ng = c/v_group;

# plot neff and ng vs lambda
f_high_res = w_high_res/(2*pi);
lambda = c/f_high_res;
plot(lambda*1e9,neff,"wavelength (nm)","neff");
setplot("show legend",false);
plot(lambda*1e9,ng,"wavelength (nm)","ng");
setplot("show legend",false);

# display the neff and ng at 1550nm
neff_2000 = interp(neff,lambda,2000e-9);
ng_2000 = interp(ng,lambda,2000e-9);
neff_1550 = interp(neff,lambda,1550e-9);
ng_1550 = interp(ng,lambda,1550e-9);
neff_1310 = interp(neff,lambda,1310e-9);
ng_1310 = interp(ng,lambda,1310e-9);

?"neff at 2000 = " + num2str(neff_2000);
?"ng at 2000 = " + num2str(ng_2000);
?"substrate index at 2000 = " + num2str(real(interp(n_substrate,lightline_w/(2*pi),c/2000e-9)));

?"neff at 1550 = " + num2str(neff_1550);
?"ng at 1550 = " + num2str(ng_1550);
?"substrate index at 1550 = " + num2str(real(interp(n_substrate,lightline_w/(2*pi),c/1550e-9)));

?"neff at 1310 = " + num2str(neff_1310);
?"ng at 1310 = " + num2str(ng_1310);
?"substrate index at 1310 = " + num2str(real(interp(n_substrate,lightline_w/(2*pi),c/1310e-9)));
