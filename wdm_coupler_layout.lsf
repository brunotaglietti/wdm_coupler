# deleteall;

## draw cladding
addrect;
set("name","Clad");  set("material", Clad_Material);
set("x min", -substrate_length/2);
set("x max", substrate_length/2);
set("y", 0);
set("y span", substrate_width);
set("z min", 0);
set("z max", 2e-6);
set("override mesh order from material database",1);
set("mesh order",3);  # similar to "send to back", put the cladding as a background.
set("alpha", 0.3);

## draw buried oxide
addrect;
set("name", "BOX"); set("material", Box_Material);
set("x min", -substrate_length/2);
set("x max", substrate_length/2);
set("y", 0);
set("y span", substrate_width);
set("z min", -3e-6); set("z max", 0);
set("override mesh order from material database",1);
set("mesh order",3);  # similar to "send to back", put the cladding as a background.
set("alpha", 0.3);

## draw silicon wafer
addrect;
set("name", "Wafer"); set("material", Core_Material);
set("x min", -substrate_length/2);
set("x max", substrate_length/2);
set("y", 0);
set("y span", substrate_width);
set("z min", -6e-6); set("z max", -3e-6);
set("alpha", 0.3);

## Adds the top SWG bus
addstructuregroup;
set("name", "WDM Coupler");
set("x",0);
set("y", 0);
set("z",0);
adduserprop("Clad_Material", 5, 'SiO2 (Glass) - Palik');
adduserprop("Box_Material", 5, 'SiO2 (Glass) - Palik');
adduserprop("Core_Material", 5, 'Si (Silicon) - Palik');
adduserprop("substrate_length", 2, substrate_length);
adduserprop("substrate_width", 2, substrate_width);
adduserprop("wi", 2, wi);
adduserprop("Lc", 2, Lc);
adduserprop("Lt", 2, Lt);
adduserprop("Lb", 2, Lb);
adduserprop("ws", 2, ws);
adduserprop("ws2", 2, ws2);
adduserprop("Lambda", 2, Lambda);
adduserprop("a", 2, a);
adduserprop("g", 2, g);
adduserprop("thickness", 2, thickness);
adduserprop("overetch", 2, overetch);
adduserprop("sidewall_angle", 0, sidewall_angle);
set("construction group", 1);
set('script', read("wdm_coupler_struc.lsf"));

## WDM Coupler design is finished

## Adds the FDTD simulation area and parameters
if(fileextension(currentfilename)=="lms") {addvarfdtd; set("simulation time", 8e-12);}
if(fileextension(currentfilename)=="fsp") {addfdtd; set("simulation time", 8e-12);}
set("x min", -Lc/2 - Lt - 2e-6);
set("x max", Lc/2 + Lt + Lb + 2e-6);
set("y", 0);
set("y span", 2*ws + g + Lb + 1.0e-6);
set("z", 0);
set("z span", 10*thickness);
if(fileextension(currentfilename)=="lms") {
    set("bandwidth", "broadband");
    set("maximum number of materials", 100);
    set("number of samples", 20);
    set("max coefficients", 3);
    set("fit tolerance", 0.01);
    set("imaginary weight", 1);
    set("improve stability", 1);
    set("make fit passive", 1);
    set("test points", [-Lc/4, 2*ws; Lc/4, 2*ws; -Lc/4, -2*ws; Lc/4, -2*ws]);
    set("x0", 0);
    set("y0", -g/2 - ws/2);
    set('mesh accuracy', 4);
    set('min mesh step', 1e-9);
}
set("z min bc", "Symmetric");
set("z max bc", "PML");
set("allow symmetry on all boundaries", 1);
set('pml profile', 3);
set('auto shutoff min', 1e-5);

## Determines the mesh area
addmesh;
set("dx", 0.020e-6);
set("dy", 0.020e-6);
set("dz", 0.020e-6);
set("x min", -Lc/2 - Lt - 2e-6);
set("x max", Lc/2 + Lt + Lb + 2e-6);
set("y", 0);
set("y span", 2*ws + g + Lb + 0.5e-6);
set("z", 0);
set("z span", 3*thickness);

## Adds the source
if(fileextension(currentfilename)=="lms") {addmodesource;}
if(fileextension(currentfilename)=="fsp") {addmode;}
set("injection axis", "x-axis");
set("direction", "forward");
if(fileextension(currentfilename)=="lms") {
    set("mode selection", "user select");
    set("selected mode number", 1);
    updatesourcemode(1);
}
if(fileextension(currentfilename)=="fsp") {set("mode selection", "fundamental TE mode");}
set("x", -Lc/2 - Lt - 1e-6);
set("y", -g/2 - ws/2);
set("y span", 10*wi);
set("wavelength start", 1195e-9);
set("wavelength stop", 1675e-9);

## Top View Power Profile
addprofile;
set("name", "Top View Power Profile");
set("monitor type", "2D Z-normal");
set("x min", -Lc/2 - Lt -2e-6);
set("x max", Lc/2 + Lt + 2e-6);
set("y", 0);
set("y span", g + 2*ws + 2e-6);
set("z", 0);

## Port 1 Results
addpower;
set("name", "P1");
set("monitor type", "2D X-normal");
set("x", Lc/2 + Lt + Lb + 1e-6);
set("y", Lb/2);
set("y span", 3*wi);
set("z", 0);
set("z span", 3*thickness);
set('override global monitor settings', 1);
set('frequency points', 251);

## Port 2 Results
addpower;
set("name", "P2");
set("monitor type", "2D X-normal");
set("x", Lc/2 + Lt + Lb + 1e-6);
set("y", -Lb/2);
set("y span", 3*(wi+g));
set("z", 0);
set("z span", 3*thickness);
set('override global monitor settings', 1);
set('frequency points', 251);
